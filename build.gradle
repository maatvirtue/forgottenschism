//-------------------------------Plugins-------------------------------
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

//------------------------General Configuration------------------------
defaultTasks 'tasks'

group = 'net.forgottenschism'
version = '2.0.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
	//Slick2D
	compile 'org.slick2d:slick2d-core:1.0.1'
	
	//Logging
    compile 'org.slf4j:slf4j-api:1.7.12'
	compile 'ch.qos.logback:logback-classic:1.1.3'

	//Generic
	compile 'org.apache.commons:commons-lang3:3.4'
	compile 'com.google.code.gson:gson:2.3.1'
}

repositories {
    mavenCentral()
}

//------------------------------Tasks------------------------------

task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}

task fatJar(type: Jar) {
	manifest {
		attributes "Main-Class": 'net.forgottenschism.main.Main'
	}
	
	baseName = project.name + '-all'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

task gatherRelease(type: Copy) {
	dependsOn fatJar
	
	from 'releaseFiles'
	from fatJar.archivePath
	
	into 'build/release/'+project.name+'-'+project.version
	
	rename(fatJar.archiveName, project.name+'.jar')
}

task zipRelease(type: Zip) {
	dependsOn gatherRelease
	
	from gatherRelease.destinationDir
	destinationDir file('build/release/')
	
	archiveName = project.name+'-'+project.version+'.zip'
}

task release() {
	dependsOn zipRelease
}

task run(type: Exec) {
	dependsOn gatherRelease
	
	workingDir gatherRelease.destinationDir
	
	if (System.getProperty('os.name').toLowerCase().contains('windows')) {
	    commandLine 'cmd', '/c', project.name+'.bat'
	}
	else {
	    commandLine './'+project.name
	}
}
